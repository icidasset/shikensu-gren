module Shikensu exposing ( Program, Task, list, program, programs, read, write )

{-|

# ðŸš€

@docs program, programs, Program, list, Task


# IO

@docs read, write

-}

import Bytes.Encode
import FileSystem
import Node.Program as Node
import Shikensu.Bundle as Bundle exposing ( Bundle )
import Shikensu.Definition as Definition exposing ( Definition )
import Shikensu.Error exposing ( Error(..) )
import Shikensu.Focus as Focus exposing ( Focus(..) )
import Shikensu.Path as Path exposing ( Path, Kind(..), Encapsulated, Directory, File )
import Task



-- ðŸ§©


{-| Alias for a Node program.
-}
type alias Program =
    Node.Program {} {}


{-| Alias for the main `Task` type we'll be working with here.
-}
type alias Task =
    Task.Task Error Bundle



-- ðŸš€


{-| Create a Shikensu Program.

This is basically a wrapper around `list` that creates a Program
for you and initialises the needed permissions.

    import Shikensu
    import Shikensu.Contrib as Shikensu
    import Shikensu.Focus exposing (Focus(..))
    import Task

    Shikensu.program sequence CurrentDirectory

    sequence task =
        task
            |> Task.map (Shikensu.withExtension "md")
            |> Task.andThen Shikensu.read
            |> Task.map (Shikensu.renderContent markdownRenderer) -- See `example` folder
            |> Task.andThen (Shikensu.write destinationFocus)


-}
program : (Task -> Task) -> Focus -> Program
program sequence focus =
    programs
        [ { focus = focus
          , sequence = sequence
          }
        ]


{-| Provides a way to make and operate on multiple lists.

Technically not multiple programs, but same idea as
the `program` function, just with multiple lists.

    import Shikensu
    import Shikensu.Focus exposing (Focus(..))

    Shikensu.programs
        [ { focus = Relative (Path.directory [ "posts" ])
          , sequence = Task.andThen Shikensu.read
          }
        , { focus = Relative (Path.directory [ "images" ])
          , sequence = Task.map (\bundle -> ...)
          }
        ]

-}
programs :
    Array
        { focus : Focus
        , sequence : Task -> Task
        }
    -> Program
programs collection =
    (\fsPermission ->
        collection
            |> Array.map
                    (\{ focus, sequence } ->
                        focus
                            |> list fsPermission
                            |> sequence
                    )
            |> Task.sequence
            |> Task.attempt (\_ -> {})
            |> Node.endWithCmd
    )
        |> Node.await FileSystem.initialize
        |> Node.defineSimple


{-| The `list` function itself, unwrapped.

Recursively lists a directory and then gives you a `Task`
containing a `Bundle` which in turns has an array of `Definition`s
stored under the property named `compendium`.

This collection of `Definition`s is the main thing you'll be working with.
The goal of this library is to scan a file tree, manipulate it and then
optionally write it back to disk. This is done through these definitions.

    import Shikensu
    import Shikensu.Focus exposing (Focus(..))

    Shikensu.list fsPermission CurrentDirectory

âš ï¸ The given `Focus` will always be in the context of
the environment of the resulting Gren binary that is being executed.
So for example, say your `pwd` command returns `~/code/shikensu/`,
and your focus is `Relative (Path.Directory [ "..", "example" ])`.
If you run `./binary/app` the resulting path will be `~/code/example/`.

-}
list : FileSystem.Permission -> Focus -> Task
list fsPermission focus =
    fsPermission
        |> Path.currentWorkingDirectory
        |> Task.mapError neverError
        |> Task.andThen
                (\cwd ->
                    let
                        focusDirectory =
                            Focus.toAbsolutePath
                                { cwd = cwd
                                }
                                focus
                    in
                    []
                        |> Path.directory
                        |> recursiveList fsPermission focusDirectory
                        |> Task.map
                                (\compendium ->
                                    { compendium = compendium
                                    , focusDirectory = focusDirectory
                                    , fsPermission = fsPermission
                                    , workingDirectory = cwd
                                    }
                                )
                )



-- IO


{-| Read the files in the given compendium/bundle,
setting the `content` property in the definition.
-}
read : Bundle -> Task
read bundle =
    bundle.compendium
        |> Array.map
                (\def ->
                    def
                        |> Definition.relativePath
                        |> Path.combine bundle.focusDirectory
                        |> (\path ->
                                path
                                    |> Path.toPosix
                                            { absolute = True
                                            }
                                    |> FileSystem.openForRead bundle.fsPermission
                                    |> Task.mapError (PlatformAccessError (Path.encapsulate path))
                            )
                        |> Task.andThen
                                (\handle ->
                                    handle
                                        |> FileSystem.read
                                        |> Task.mapError PlatformUnknownError
                                        |> Task.map (\bytes -> { def | content = Just bytes })
                                        |> Task.andThen
                                                (\updatedDef ->
                                                    handle
                                                        |> FileSystem.close
                                                        |> Task.map (\_ -> updatedDef)
                                                        |> Task.mapError PlatformUnknownError
                                                )
                                )
                )
        |> Task.sequence
        |> Task.map (\compendium -> { bundle | compendium = compendium })


{-| Write each definition to their respective location.
The location will depend on the focus and the environment it was run in.
-}
write : Focus -> Bundle -> Task
write destinationFocus bundle =
    destinationFocus
        |> Focus.toAbsolutePath
                { cwd = bundle.workingDirectory
                }
        |> (\destinationDirectory ->
                bundle.compendium
                    |> Array.map (writeDef bundle.fsPermission destinationDirectory)
                    |> Task.sequence
                    |> Task.map (\_ -> bundle)
            )


writeDef : FileSystem.Permission -> Path Directory -> Definition -> Task.Task Error {}
writeDef permission destinationDirectory def =
    def
        |> Definition.relativePath
        |> Path.combine destinationDirectory
        |> (\path ->
                path
                    |> Path.toPosix
                            { absolute = True
                            }
                    |> FileSystem.openForWrite permission FileSystem.EnsureEmpty
                    |> Task.mapError (PlatformAccessError (Path.encapsulate path))
            )
        |> Task.andThen
                (\handle ->
                    def.content
                        |> Maybe.withDefault
                                (Bytes.Encode.encode
                                    <| Bytes.Encode.string ""
                                )
                        |> FileSystem.write handle
                        |> Task.mapError PlatformUnknownError
                )
        |> Task.map (\_ -> {})
        |> Task.onError
                (\err ->
                    case err of
                        PlatformAccessError path FileSystem.AccessErrorNotFound ->
                            -- Make sure directory tree exists
                            let
                                directoryPath =
                                    path
                                        |> Path.unwrap
                                        |> Array.dropLast 1
                                        |> Path.directory

                                directoryPathString =
                                    Path.toPosix
                                        { absolute = True
                                        }
                                        directoryPath
                            in
                            { recursive = True
                            }
                                |> FileSystem.makeDirectory permission directoryPathString
                                |> Task.mapError (PlatformAccessError (Path.encapsulate directoryPath))
                                |> Task.andThen (\_ -> writeDef permission destinationDirectory def)

                        _ ->
                            Task.fail err
                )



-- ãŠ™ï¸


neverError : Never -> Error
neverError _ =
    PlatformUnknownError (FileSystem.UnknownFileSystemError "Never ever have I ðŸ¤«")


recursiveList : FileSystem.Permission -> Path Directory -> Path Directory -> Task.Task Error (Array Definition)
recursiveList permission focusDirectory relativePath =
    relativePath
        |> Path.combine focusDirectory
        |> (\path ->
                path
                    |> Path.toPosix
                            { absolute = True
                            }
                    |> FileSystem.listDirectory permission
                    |> Task.mapError (PlatformAccessError (Path.encapsulate path))
            )
        |> Task.andThen
                (\listing ->
                    listing
                        |> Array.filterMap
                                (\entry ->
                                    -- TODO: Add option to also list hidden files
                                    case entry of
                                        FileSystem.File name ->
                                            if String.startsWith "." name then
                                                Nothing
                                            else
                                                name
                                                    |> Array.singleton
                                                    |> Path.file
                                                    |> Path.combine relativePath
                                                    |> Definition.create
                                                    |> Array.singleton
                                                    |> Task.succeed
                                                    |> Just

                                        FileSystem.Directory name ->
                                            name
                                                |> Array.singleton
                                                |> Path.directory
                                                |> Path.combine relativePath
                                                |> recursiveList permission focusDirectory
                                                |> Just

                                        _ ->
                                            Nothing
                                )
                        |> Task.sequence
                        |> Task.map Array.flatten
                )
