module Shikensu.Definition exposing ( Definition, create, fork, relativePath )

{-|

# Definition

@docs Definition, create, fork, relativePath

-}

import Bytes exposing ( Bytes )
import Dict exposing ( Dict )
import Json.Encode as Json
import Shikensu.Focus as Focus exposing ( Focus )
import Shikensu.Path as Path exposing ( Path, Kind(..), Directory, File )



-- ðŸ§©


{-| **A piece of content.**

Example definition, given:
The working directory root path `/Users/icidasset/Projects/shikensu/example/`
and the full item path `/Users/icidasset/Projects/shikensu/example/test/hello.md`

    { baseName = "hello"
    , content = Nothing
    , directoryPath = Path.directory [ "test" ]
    , extensionName = Just "md"
    , metadata = Dict.empty
    }

-}
type alias Definition =
    { baseName : String
    , content : Maybe Bytes
    , directoryPath : Path Directory
    , extensionName : Maybe String
    , metadata : Dict String Json.Value
    }



-- PATHS


{-| The path relative to the working directory.
-}
relativePath : Definition -> Path File
relativePath def =
    [ def.baseName ++ Maybe.withDefault "" (Maybe.map (\e -> "." ++ e) def.extensionName)
    ]
        |> Path.file
        |> Path.combine def.directoryPath



-- ðŸ› 


{-| Create a definition, given a relative path.
-}
create : Path File -> Definition
create relPath =
    let
        name =
            relPath
                |> Path.unwrap
                |> Array.last
                |> Maybe.withDefault ""
    in
    { baseName = baseName name
    , content = Nothing
    , directoryPath = directoryPath relPath
    , extensionName = extensionName name
    , metadata = Dict.empty
    }


{-| Fork a definition, given a relative path.
Taking the metadata and content of the original definition.
-}
fork : Path File -> Definition -> Definition
fork relPath def =
    let
        name =
            relPath
                |> Path.unwrap
                |> Array.last
                |> Maybe.withDefault ""
    in
    { baseName = baseName name
    , content = def.content
    , directoryPath = directoryPath relPath
    , extensionName = extensionName name
    , metadata = def.metadata
    }



-- ãŠ™ï¸


baseName : String -> String
baseName name =
    if String.contains "." name then
        name
            |> String.split "."
            |> Array.dropLast 1
            |> String.join "."
    else
        name


directoryPath : Path File -> Path Directory
directoryPath relPath =
    relPath
        |> Path.unwrap
        |> Array.dropLast 1
        |> Path.directory


extensionName : String -> Maybe String
extensionName name =
    if String.contains "." name then
        Array.last (String.split "." name)
    else
        Nothing
